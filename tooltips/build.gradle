apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
  compileSdkVersion androidCompileSdkVersion
  buildToolsVersion androidBuildToolsVersion

  defaultConfig {
    minSdkVersion androidMinSdkVersion
    targetSdkVersion androidTargetSdkVersion
  }

  compileOptions {
    sourceCompatibility javaVersion
    targetCompatibility javaVersion
  }
}

dependencies {
  compileOnly "androidx.annotation:annotation:1.1.0"
}

tasks.register("sourcesJar", Jar) {
  archiveClassifier = "sources"
  from(android.sourceSets.main.java.srcDirs)
}

tasks.register("androidJavadocs", Javadoc) {
  source(android.sourceSets.main.java.sourceFiles)
  classpath += project.files(android.bootClasspath.join(File.pathSeparator))
  android.libraryVariants.each {
    if (it.name == "release") {
      classpath += it.javaCompileProvider.get().classpath
    }
  }
  options {
    docTitle "Tooltips for Android"
    windowTitle docTitle
    source = "8"
    links "https://developer.android.com/reference/"
  }
}

tasks.register("javadocJar", Jar) {
  archiveClassifier = "javadoc"
  from(tasks.named("androidJavadocs").map { it.destinationDir })
  dependsOn tasks.named("androidJavadocs")
}

afterEvaluate {
  def properties = new Properties()
  def file = rootProject.file("local.properties")
  if (file.isFile()) {
    file.withInputStream { stream ->
      properties.load(stream)
    }
  }

  publishing {
    publications {
      maven(MavenPublication) {
        groupId "io.github.minyushov"
        artifactId = "tooltips"
        version = "2.1.0"

        from(components.findByName("release"))
        artifact(tasks.named("sourcesJar"))
        artifact(tasks.named("javadocJar"))

        pom {
          name = artifactId
          description = "Tooltips for Android"
          url = "https://github.com/minyushov/tooltips"
          developers {
            developer {
              id = "minyushov"
              name = "Semyon Minyushov"
              email = "minyushov@gmail.com"
            }
          }
          licenses {
            license {
              name = "Apache-2.0"
              url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
            }
          }
          scm {
            connection = "https://github.com/minyushov/tooltips.git"
            developerConnection = "https://github.com/minyushov/tooltips.git"
            url = "https://github.com/minyushov/tooltips/tree/master"
          }
        }
      }
    }
    repositories {
      maven {
        name "sonatype"
        url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        credentials {
          username properties["SONATYPE_USER"]
          password properties["SONATYPE_PASSWORD"]
        }
      }
    }
  }

  signing {
    useInMemoryPgpKeys(
      properties["GPG_KEY_ID"],
      properties["GPG_KEY"],
      properties["GPG_PASSWORD"]
    )
    sign(publishing.publications)
  }
}